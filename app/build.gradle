apply plugin: 'com.android.application'
apply plugin: 'jacoco'

task jacocoTestReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(files("${buildDir}/jacoco/testDebugUnitTest.exec"))
}

def getProperty(String filename, String propName) {
    def propsFile = rootProject.file(filename)
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        if (props[propName] != null) {
            return props[propName]
        } else {
            print("No such property " + propName + " in file " + filename)
        }
    } else {
        return ("\$" + propName)
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.sophieoc.go4lunch"
        minSdkVersion 19
        targetSdkVersion 29
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
        resConfigs "en", "fr"
        buildConfigField "String", 'API_KEY',  "${getProperty("gradle.properties", "API_KEY")}"
        resValue "string", 'API_KEY', "${getProperty("gradle.properties", "API_KEY")}"
        buildConfigField "String", 'ALGOLIA_API_KEY', "${getProperty("gradle.properties", "ALGOLIA_API_KEY")}"
        buildConfigField "String", 'ALGOLIA_APP_ID', "${getProperty("gradle.properties", "ALGOLIA_APP_ID")}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
       unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        checkReleaseBuilds false
        disable 'RestrictedApi'
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    viewBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.3.0-alpha01'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha03'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.13'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'

    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.ext:truth:1.2.0'
    androidTestImplementation 'com.google.truth:truth:0.44'

    // Espresso dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.2.0'
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:3.2.0'

    // FirebaseUI
    implementation 'com.firebaseui:firebase-ui-storage:6.2.1'
    implementation 'com.firebaseui:firebase-ui-auth:6.2.1'
    implementation 'com.firebaseui:firebase-ui-firestore:6.2.1'

    // Firebase
    implementation 'com.google.firebase:firebase-auth:19.3.1'
    implementation 'com.google.firebase:firebase-firestore:21.4.3'
    implementation 'com.google.firebase:firebase-analytics:17.4.3'

    implementation 'com.google.android.gms:play-services-auth:18.0.0'

    // Google Play Service : Map
    implementation 'com.google.android.gms:play-services-maps:17.0.0'

    // Google Play Service : Location
    implementation 'com.google.android.gms:play-services-location:17.0.0'

    // Google Places
    implementation 'com.google.android.libraries.places:places:2.3.0'

    // FACEBOOK LOGIN SUPPORT
    implementation 'com.facebook.android:facebook-android-sdk:5.15.3'

    // GSON
    implementation 'com.google.code.gson:gson:2.8.6'

    // GLIDE
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.1'

    // ViewModel
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.2.0'

    // LiveData
    implementation 'androidx.lifecycle:lifecycle-livedata:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // WorkManager
    implementation "androidx.work:work-runtime:2.3.4"


    // To fix build error :
    // "Duplicate class com.google.common.util.concurrent.ListenableFuture found in modules
    // jetified-guava-26.0-android.jar (com.google.guava:guava:26.0-android)
    // and jetified-listenablefuture-1.0.jar (com.google.guava:listenablefuture:1.0)"
    implementation 'com.google.guava:guava:27.0.1-android'

    // To help calculate RectangularBounds
    implementation 'com.google.maps.android:android-maps-utils:1.3.1'

    // EasyPermissions
    implementation 'pub.devrel:easypermissions:3.0.0'

    // Algolia
    implementation 'com.algolia:algoliasearch-android:3.27.0'

    // Mockito
    testImplementation "org.mockito:mockito-core:2.28.2"

    // MockWebServer
    testImplementation("com.squareup.okhttp3:mockwebserver:4.7.2")

    // Robolectric
    testImplementation 'org.robolectric:robolectric:4.3'

}

apply plugin: 'com.google.gms.google-services'

